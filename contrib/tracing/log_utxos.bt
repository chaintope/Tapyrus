#!/usr/bin/env bpftrace

/*

  USAGE:

  bpftrace contrib/tracing/log_utxos.bt

  This script requires a 'tapyrusd' binary compiled with eBPF support and the
  'utxocache' tracepoints. By default, it's assumed that 'tapyrusd' is
  located in './src/tapyrusd'. This can be modified in the script below.

  NOTE: requires bpftrace v0.12.0 or above.
*/

BEGIN
{
    printf("%-7s %-70s %30s %16s %7s %8s\n",
          "OP", "Outpoint", "Token",  "Value", "Height", "Coinbase");
}

/*
  Attaches to the 'utxocache_add' tracepoint and prints additions to the UTXO set cache.
*/
usdt:./src/tapyrusd:utxocache_add
{
  @txid = str(arg0);
  $index = (uint32)arg1;
  @token = str(arg2);
  $height = (uint32)arg3;
  $value = (int64)arg4;
  $isCoinbase = arg5;

  printf("Added  ");
  printf(":%s %-6d %s %16ld %7d %s\n", @txid, $index, @token, $value, $height, ($isCoinbase ? "Yes" : "No" ));
}

/*
  Attaches to the 'utxocache_spent' tracepoint and prints spents from the UTXO set cache.
*/
usdt:./src/tapyrusd:utxocache_spent
{
  @txid = str(arg0);
  $index = (uint32)arg1;
  @token = str(arg2);
  $height = (uint32)arg3;
  $value = (int64)arg4;
  $isCoinbase = arg5;

  printf("Spent   ");
  printf(":%s %-6d %s %16ld %7d %s\n", @txid, $index, @token, $value, $height, ($isCoinbase ? "Yes" : "No" ));
}

/*
  Attaches to the 'utxocache_uncache' tracepoint and uncache UTXOs from the UTXO set cache.
*/
usdt:./src/tapyrusd:utxocache_uncache
{
  @txid = str(arg0);
  $index = (uint32)arg1;
  @token = str(arg2);
  $height = (uint32)arg3;
  $value = (int64)arg4;
  $isCoinbase = arg5;

  printf("Uncache ");
  printf(":%s %-6d %s %16ld %7d %s\n", @txid, $index, @token, $value, $height, ($isCoinbase ? "Yes" : "No" ));
}
